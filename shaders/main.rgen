#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require

#include "structures.h"

layout(set = 0, binding = 0) uniform accelerationStructureEXT topLevelAS;
layout(set = 1, binding = 0, rgba8) uniform image2D image;
layout(set = 2, binding = 0) uniform CameraUBO {
    mat4 projection;
    mat4 view;
    mat4 projectionInv;
    mat4 viewInv;
} camera;

layout(location = 0) rayPayloadEXT vec3 payLoad;

void main()
{
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5f, 0.5f);
    const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);
    vec2 d = inUV * 2.0 - 1.0;

    vec4 origin    = camera.viewInv       * vec4(0, 0, 0, 1);
    vec4 target    = camera.projectionInv * vec4(d.x, d.y, 1, 1);
    vec4 direction = camera.viewInv       * vec4(normalize(target.xyz), 0);

    payLoad = origin.xyz;
    traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xff, 0, 0, 0, origin.xyz, 0.001f, direction.xyz, 10000.0f, 0);
    imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(payLoad, 1.0f));
}

